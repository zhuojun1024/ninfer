// ninfer::ops - embedding launcher: variant grid/block/stream setup.
#include "core/weight.h"
#include "ops/launcher/embed_gather.h"

#include "ops/common/math.h"
#include "ops/kernel/embed_gather.cuh"
#include "core/device.h" // CUDA_CHECK

#include <algorithm>
#include <cstdint>
#include <stdexcept>

namespace ninfer::ops::detail {
namespace {

constexpr int kBlock          = 128;
constexpr int kQ6GroupedBlock = kEmbedGatherQ6Group * kEmbedGatherQ6GroupsPerBlock;
constexpr int kQ8GroupedBlock = 32;
constexpr int kQ8RowBlock     = 256;

template <int D, int BlocksPerToken, int Threads>
void launch_fp8(const Tensor& ids, const Weight& table, Tensor& out, cudaStream_t stream) {
    const int grid = ids.ne[0] * BlocksPerToken;
    embed_gather_fp8_kernel<D, BlocksPerToken, Threads><<<grid, Threads, 0, stream>>>(
        static_cast<const std::int32_t*>(ids.data), static_cast<const std::uint8_t*>(table.qdata),
        static_cast<const __nv_bfloat16*>(table.scales), static_cast<__nv_bfloat16*>(out.data));
}

template <int Blocks, int Threads>
void launch_q8_packed(const Tensor& ids, const Weight& table, Tensor& out, cudaStream_t stream) {
    const auto launch = [&]<bool PairStore>() {
        embed_gather_q8_packed_5120_kernel<Blocks, Threads, PairStore>
            <<<ids.ne[0] * Blocks, Threads, 0, stream>>>(
                static_cast<const std::int32_t*>(ids.data),
                static_cast<const std::uint8_t*>(table.qdata),
                static_cast<const std::uint8_t*>(table.scales),
                static_cast<__nv_bfloat16*>(out.data));
    };
    if (reinterpret_cast<std::uintptr_t>(out.data) % 4 == 0)
        launch.template operator()<true>();
    else
        launch.template operator()<false>();
}

int grid_for(std::int64_t n) {
    return static_cast<int>(
        std::max<std::int64_t>(1, div_up(n, static_cast<std::int64_t>(kBlock))));
}

int grid_for_q6_grouped(std::int32_t d, std::int32_t T) {
    const std::int32_t kg           = d / kEmbedGatherQ6Group;
    const std::int32_t group_blocks = div_up(kg, kEmbedGatherQ6GroupsPerBlock);
    return static_cast<int>(std::max<std::int64_t>(1, static_cast<std::int64_t>(T) *
                                                          static_cast<std::int64_t>(group_blocks)));
}

} // namespace

const char* q8_embed_route_name(Q8EmbedRoute route) {
    switch (route) {
    case Q8EmbedRoute::Auto:
        return "auto";
    case Q8EmbedRoute::Grouped:
        return "grouped-b32";
    case Q8EmbedRoute::Row:
        return "row-b256";
    }
    return "unknown";
}

void embed_gather_q8_2048_launch(const Tensor& ids, const Weight& table, Tensor& out,
                                 Q8EmbedRoute route, cudaStream_t stream) {
    const std::int32_t T = ids.ne[0];
    const auto* codes    = static_cast<const std::uint8_t*>(table.qdata);
    const auto* scales   = static_cast<const std::uint8_t*>(table.scales);
    if (route == Q8EmbedRoute::Auto) { route = T <= 6 ? Q8EmbedRoute::Grouped : Q8EmbedRoute::Row; }
    if (route == Q8EmbedRoute::Grouped) {
        const int grid = T * kEmbedGatherQ8Groups;
        embed_gather_q8_grouped_2048_kernel<<<grid, kQ8GroupedBlock, 0, stream>>>(
            static_cast<const std::int32_t*>(ids.data), codes, scales,
            static_cast<__nv_bfloat16*>(out.data));
    } else {
        embed_gather_q8_row_2048_kernel<<<T, kQ8RowBlock, 0, stream>>>(
            static_cast<const std::int32_t*>(ids.data), codes, scales,
            static_cast<__nv_bfloat16*>(out.data));
    }
    CUDA_CHECK(cudaGetLastError());
}

void embed_gather_dense_launch(const Tensor& ids, const Tensor& table, Tensor& out,
                               cudaStream_t stream) {
    const std::int32_t d = out.ne[0];
    const std::int32_t T = ids.ne[0];
    const std::int64_t n = static_cast<std::int64_t>(d) * T;
    embed_gather_dense_kernel<<<grid_for(n), kBlock, 0, stream>>>(
        static_cast<const std::int32_t*>(ids.data), static_cast<const __nv_bfloat16*>(table.data),
        static_cast<__nv_bfloat16*>(out.data), d, T);
    CUDA_CHECK(cudaGetLastError());
}

void embed_gather_q6_launch(const Tensor& ids, const Weight& table, Tensor& out,
                            cudaStream_t stream) {
    const std::int32_t d = out.ne[0];
    const std::int32_t T = ids.ne[0];
    const std::int64_t n = static_cast<std::int64_t>(d) * T;
    const auto* codes    = static_cast<const std::uint8_t*>(table.qdata);
    const auto* high     = static_cast<const std::uint8_t*>(table.qhigh);
    const auto* scales   = static_cast<const std::uint8_t*>(table.scales);
    if (d == table.padded_shape[1] && d % kEmbedGatherQ6Group == 0) {
        embed_gather_q6_grouped_kernel<<<grid_for_q6_grouped(d, T), kQ6GroupedBlock, 0, stream>>>(
            static_cast<const std::int32_t*>(ids.data), codes, high, scales,
            static_cast<__nv_bfloat16*>(out.data), d, T);
        CUDA_CHECK(cudaGetLastError());
        return;
    }

    embed_gather_q6_kernel<<<grid_for(n), kBlock, 0, stream>>>(
        static_cast<const std::int32_t*>(ids.data), codes, high, scales,
        static_cast<__nv_bfloat16*>(out.data), d, T, table.padded_shape[1]);
    CUDA_CHECK(cudaGetLastError());
}

void embed_gather_q8_launch(const Tensor& ids, const Weight& table, Tensor& out,
                            cudaStream_t stream) {
    const std::int32_t d = out.ne[0];
    const std::int32_t T = ids.ne[0];
    const auto* codes    = static_cast<const std::uint8_t*>(table.qdata);
    const auto* scales   = static_cast<const std::uint8_t*>(table.scales);
    if (d == kEmbedGatherQ8D && table.padded_shape[1] == kEmbedGatherQ8D) {
        embed_gather_q8_2048_launch(ids, table, out, Q8EmbedRoute::Auto, stream);
        return;
    }

    // Four signed codes share their exact group scale. Keep byte-addressed tables on the
    // scalar reader and preserve two-byte output alignment through the packed kernel's stores.
    if (d == 5120 && table.padded_shape[1] == 5120 &&
        reinterpret_cast<std::uintptr_t>(table.qdata) % 4 == 0) {
        if (T <= 128)
            launch_q8_packed<10, 128>(ids, table, out, stream);
        else
            launch_q8_packed<5, 128>(ids, table, out, stream);
        CUDA_CHECK(cudaGetLastError());
        return;
    }

    const std::int64_t n = static_cast<std::int64_t>(d) * T;
    embed_gather_q8_kernel<<<grid_for(n), kBlock, 0, stream>>>(
        static_cast<const std::int32_t*>(ids.data), codes, scales,
        static_cast<__nv_bfloat16*>(out.data), d, T, table.padded_shape[1]);
    CUDA_CHECK(cudaGetLastError());
}

bool embed_gather_fp8_supports_width(std::int32_t d) noexcept {
    return d == kEmbedGatherFp8D || d == kEmbedGatherFp8DHalf;
}

void embed_gather_fp8_launch(const Tensor& ids, const Weight& table, Tensor& out,
                             cudaStream_t stream) {
    const std::int32_t T = ids.ne[0];
    // One instantiation per supported hidden width. Short sequences spread one token over many
    // blocks; long sequences use fewer blocks per token so the grids stay bounded.
    switch (table.k) {
    case kEmbedGatherFp8D:
        if (T <= 176)
            launch_fp8<kEmbedGatherFp8D, 10, 128>(ids, table, out, stream);
        else
            launch_fp8<kEmbedGatherFp8D, 5, 128>(ids, table, out, stream);
        break;
    case kEmbedGatherFp8DHalf:
        if (T <= 176)
            launch_fp8<kEmbedGatherFp8DHalf, 5, 128>(ids, table, out, stream);
        else
            launch_fp8<kEmbedGatherFp8DHalf, 2, 128>(ids, table, out, stream);
        break;
    default:
        throw std::invalid_argument("embedding: unsupported FP8 table hidden width");
    }
    CUDA_CHECK(cudaGetLastError());
}

} // namespace ninfer::ops::detail
